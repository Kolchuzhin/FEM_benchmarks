!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!
! Filename: HF120element.mac, ANSYS APDL script
! by <vladimir.kolchuzhin@ieee.org>
!
! Issue: export element matrix: [12x12] complex --> file
!
! Rev.: 15:01 2019-02-22
!
!==========================================================================!
!
!      	            z    W
!       P  o-------------X-----------o O
!         /         ^               /|
!      X x          |    y      V  x |
!       /         U |   /         /  x A
!    M o------------x------------o N |
!      |            | /          |   |
!      |            |/           |   o K
!    Y x            *------------x--/-->  x
!      |                       Z | /
!      |                         |/
!    I o-----------x-------------o J
!                  Q
!
!                   ASCII-Schematic of the problem
!
! Nodes I, J, K, L, M, N, O, P, Q, R, S, T, U, V, W, X, Y, Z, A, B
!
!==========================================================================!
!
!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
!xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx!
FINISH $/CLEAR $/CONTOUR,1,128

/VUP,,Z


/TITLE, HF120element

project_name='HF120element'
!--------------------------------------------------------------------------!
/PREP7

mm=1e-3		!
um=1e-6		!


Lx=1.0*um
Ly=2.0*um
Lz=1.0*um
!-------------------------! geometry parametrs !---------------------------!
! 

x1=-Lx/2 	$y1=-Ly/2 	$z1=-Lz/2
x2=+Lx/2 	$y2=-Ly/2 	$z2=-Lz/2
x3=+Lx/2 	$y3=+Ly/2 	$z3=-Lz/2
x4=-Lx/2 	$y4=+Ly/2 	$z4=-Lz/2
x5=-Lx/2 	$y5=-Ly/2 	$z5=+Lz/2
x6=+Lx/2 	$y6=-Ly/2 	$z6=+Lz/2
x7=+Lx/2 	$y7=+Ly/2 	$z7=+Lz/2
x8=-Lx/2 	$y8=+Ly/2 	$z8=+Lz/2

x9=0 		$y9=-Ly/2	$z9 =-Lz/2
x10=+Lx/2	$y10=0		$z10=-Lz/2
x11=0 		$y11=+Ly/2 	$z11=-Lz/2
x12=-Lx/2	$y12=0	 	$z12=-Lz/2
x13=0 		$y13=-Ly/2 	$z13=+Lz/2
x14=+Lx/2	$y14=0	 	$z14=+Lz/2
x15=0 		$y15=+Ly/2 	$z15=+Lz/2
x16=-Lx/2 	$y16=0	 	$z16=+Lz/2
x17=-Lx/2	$y17=-Ly/2 	$z17=0
x18=+Lx/2	$y18=-Ly/2	$z18=0
x19=+Lx/2	$y19=+Ly/2 	$z19=0
x20=-Lx/2	$y20=+Ly/2 	$z20=0
!--------------------------------------------------------------------------!
!----------------------! Defines elements types !--------------------------!
!--------------------------------------------------------------------------!
! A first order or second order element option is available for the hexahedral and prism-shaped elements using KEYOPT(1). 
! The pyramid-shaped element is only available as a first order element. 
! The first order element has one AX DOF on each edge. The first order hexahedral element has a total of 12 AX DOFs.
! The first order prism and pyramid elements have a total of 9 AX and 8 AX DOFs, respectively. 

! The second order prism element has a total of 42 DOFs {2(9 edges) + 4(5 faces) + 4(1 volume)}.
! The second order hexahedral element has two AX DOFs on each edge, four AX DOFs on each face, 
! and six AX DOFs inside the volume for a total of 54 DOFs {2(12 edges) + 4(6 faces) + 6 (1 volume)}.

! MKS unit: free-space permeability MUZRO = 4pi x 10-7 H/m and the free-space permittivity EPZERO = 8.854 x 10-12 F/m

!HF118
!HF119
!HF120

ET,1,HF120,1                    ! HIGH FREQUENCY SOLID BRICK, 1ST ORDER
!ET,1,HF120,2                    ! HIGH FREQUENCY SOLID BRICK, 2ND ORDER

!--------------------------------------------------------------------------!
!-------------------! Defines material properties !------------------------!
!--------------------------------------------------------------------------!

MP,MURX,1,1.0                    ! RELATIVE PERMEABILITY
MP,PERX,1,1.0                    ! RELATIVE PERMITTIVITY

FREQ=1E9                        ! FREQUENCY

!--------------------------------------------------------------------------!
!------------------------------! FEA Domain !------------------------------!
!--------------------------------------------------------------------------!

MAT,1
TYPE,1

!--------------------------------------------------------------------------!

N,1,x1,y1,z1
N,2,x2,y2,z2
N,3,x3,y3,z3
N,4,x4,y4,z4
N,5,x5,y5,z5
N,6,x6,y6,z6
N,7,x7,y7,z7
N,8,x8,y8,z8

N, 9, x9, y9, z9
N,10,x10,y10,z10
N,11,x11,y11,z11
N,12,x12,y12,z12
N,13,x13,y13,z13
N,14,x14,y14,z14
N,15,x15,y15,z15
N,16,x16,y16,z16
N,17,x17,y17,z17
N,18,x18,y18,z18
N,19,x19,y19,z19
N,20,x20,y20,z20

! define an elment through nodes
E,1,2,3,4,5,6,7,8
EMORE,9,10,11,12,13,14,15,16
EMORE,17,18,19,20

! HF120 element 1 must have all of its midside nodes supplied

!--------------------------------------------------------------------------!
!--------------------! Defines components for BCs !------------------------!
!--------------------------------------------------------------------------!

 !D,1,all,0.0
 !F,5,FZ,1.0

eplot

!==========================================================================!

!save,project_name,db,,all

!==========================================================================!
/SOLU

ANTYPE,HARMIC	! FULL HARMONIC ANALYSIS
HARFRQ,FREQ

SOLVE
WRFULL,1
FINISH
!==========================================================================!
/AUX2

 !Writes an assembled global matrix in Harwell-Boeing format
 !HBMAT, Fname, Ext, --, Form, Matrx, Rhs, Mapping

file,file,full
HBMAT,HF120element,dat,,ASCII,STIFF,NO,YES	! for all types of analyses that write a .FULL file

!==========================================================================!